		; üêü —Ä–∏—Å–æ–≤–∞–Ω–∏—è –ª–∏–Ω–∏–∏ –∞–ª–≥–æ—Ä–∏—Ç–º–æ–º –ë—Ä–µ–∑–µ–Ω—Ö–µ–º–∞ –≤ —Ä–µ–∂–∏–º–µ 256—Ö256
		; –¢–µ—Å—Ç –∏ –±–µ–Ω—á–º–∞—Ä–∫
		; 
		; –î–ª—è –∑–∞–ø—É—Å–∫–∞ –±–µ–Ω—á–º–∞—Ä–∫–∞ –Ω–∞–∂–∞—Ç—å –£–° / –°–° / –†–£–°–õ–ê–¢
		;
		; –¢–æ—á–∫–∞ –≤—Ö–æ–¥–∞ –ø—Ä–æ—Ü–µ–¥—É—Ä—ã —Ä–∏—Å–æ–≤–∞–Ω–∏—è: 
		; 	line
		; –í—Ö–æ–¥–Ω—ã–µ –ø–∞—Ä–∞–º–µ—Ç—Ä—ã:
		; 	line_x0, line_y0, line_x1, line_y1
		; –†–∏—Å–æ–≤–∞–Ω–∏–µ –ø—Ä–æ–∏—Å—Ö–æ–¥–∏—Ç –≤ –ø–ª–æ—Å–∫–æ—Å—Ç–∏ 0x8000
		; –í–æ –≤—Ä–µ–º—è –æ—Å–Ω–æ–≤–Ω–æ–≥–æ —Ü–∏–∫–ª–∞ –ø—Ä–µ—Ä—ã–≤–∞–Ω–∏—è –∑–∞–ø—Ä–µ—â–µ–Ω—ã
		;
		; –í—è—á–µ—Å–ª–∞–≤ –°–ª–∞–≤–∏–Ω—Å–∫–∏–π –∏ –ò–≤–∞–Ω –ì–æ—Ä–æ–¥–µ—Ü–∫–∏–π, 2017
		;
		.binfile line5.rom 
		.nodump
		.org 100h

		rrc
		rlc
		di
		jmp 	start
		.org 200h
PixelMask:
		.db 10000000b
		.db 01000000b
		.db 00100000b
		.db 00010000b
		.db 00001000b
		.db 00000100b
		.db 00000010b
		.db 00000001b

; ---
start:
		xra	a
		out	10h
		lxi	sp,100h
		mvi	a,0C3h
		sta	0
		lxi	h,Restart
		shld	1

		call	Cls
		mvi	a,0C9h
		sta	38h
		ei
		hlt
		lxi	h, colors+15
colorset:
		mvi	a, 88h
		out	0
		mvi	c, 15
colorset1:	mov	a, c
		out	2
		mov	a, m
		out	0Ch
		dcx	h
		out	0Ch
		out	0Ch
		dcr	c
		out	0Ch
		out	0Ch
		out	0Ch
		jp	colorset1
		mvi	a,255
		out	3


Restart:
		call	Cls


		lxi h,circl
circloop:
		push h
		lxi h,8080h
		shld line_x0
		pop h
		mov a,m
		ora a
		jz benchmark
		sta line_x1
		inx h
		mov a,m
		sta line_y1
		inx h
		push h
		call line
		pop h
		jmp circloop

circl:
		.db 228,128
		.db 220,166
		.db 199,199
		.db 166,220
		.db 128,228
		.db 90,220
		.db 57,199
		.db 36,166
		.db 28,128
		.db 36,90
		.db 57,57
		.db 90,36
		.db 128,28
		.db 166,36
		.db 199,57
		.db 220,90
		.db 0

benchmark
		in 1
		rlc
		jnc benchmark_go
		rlc
		jnc benchmark_go
		rlc
		jc benchmark

benchmark_go

		;call line
		;jmp $
		call rnd16
		shld line_tail
foreva:
		lhld line_tail
		shld line_x0

		call rnd16
		mov d, h
		mov e, l
		inx d
		mov a, d
		ora e
		jz foreva_nomoar
		shld line_x1
		shld line_tail

		call line
		jmp foreva
foreva_nomoar
		jmp Restart

line_tail:	.dw 0

		; –∞—Ä–≥—É–º–µ–Ω—Ç—ã line()
line_x0		.db 100
line_y0		.db 55
line_x1		.db 0
line_y1		.db 50 

		; —ç—Ç–∏ —á–µ—Ç—ã—Ä–µ –±–∞–π—Ç–∞ –¥–æ–ª–∂–Ω—ã –∏–¥—Ç–∏ –≤ —Ç–∞–∫–æ–º –ø–æ—Ä—è–¥–∫–µ, –∞ —Ç–æ
line_y		.db 0
line_x		.db 0
line_dx 	.db 0
line_dy		.db 0


line:		; –≤—ã—á–∏—Å–ª–∏—Ç—å line_dx, line_dy –∏ –ø—Ä–∏—Ä–∞—â–µ–Ω–∏–µ Y
		; line_dx >= 0, line_dy >= 0, line1_mod_yinc ? [-1,1]
		call line_calc_dx 
		call line_calc_dy

		; –ø—Ä–æ–≤–µ—Ä—è–µ–º –∫—Ä—É—Ç–∏–∑–Ω—É —Å–∫–ª–æ–Ω–∞:
		; dy >= 0, dx >= 0
		;  	dy < dx 	?	–ø–æ–ª–æ–≥–∏–π
		;	dy >= dx 	?	–∫—Ä—É—Ç–æ–π
		lhld line_dx 	; l = dx, h = dy
		mov a, l 
		cmp h
		jnc  line_gentle
		
		; –º–µ–Ω—è–µ–º –º–µ—Å—Ç–∞–º–∏ x0 –∏ y0
		lhld line_x0 		;  l = x, h = y
		mov d, l 	 	;  d = y
		mov e, h 	 	;  e = x
		xchg		 	;  l = y, h = x
		shld line_x0

		; –º–µ–Ω—è–µ–º –º–µ—Å—Ç–∞–º–∏ x1 –∏ y1
		lhld line_x1 		;  l = x, h = y
		mov d, l 		;  d = y
		mov e, h 		;  e = x
		xchg			;  l = y, h = x
		shld line_x1
		; –∫—Ä—É—Ç–æ–π —Å–∫–ª–æ–Ω: –ø–µ—Ä–µ–≤–æ–¥–∏–º —Å—Ç—Ä–µ–ª–∫—É –Ω–∞ —Ü–∏–∫–ª S
		mvi a,0C3h		; jmp
		.db 21h			; lxi h, - –ø—Ä–æ–ø—É—Å–∫–∞–µ–º –ø–µ—Ä–µ–∫–ª—é—á–µ–Ω–∏–µ –Ω–∞ line_gentle
line_gentle:
		; —Å–∫–ª–æ–Ω –ø–æ–ª–æ–≥–∏–π: —Å—Ç—Ä–µ–ª–∫–∞ –Ω–∞ —Ü–∏–∫–ª–µ G
		mvi a,021h		; lxi h,
		sta line1_switch
		; –µ—Å–ª–∏ —Ç–µ–ø–µ—Ä—å –ø–æ–ª—É—á–∏–ª–æ—Å—å —Ç–∞–∫, —á—Ç–æ x0 > x1,
		; –Ω–∞–¥–æ –∏–∑–º–µ–Ω–∏—Ç—å –Ω–∞–ø—Ä–∞–≤–ª–µ–Ω–∏–µ —Ä–∏—Å–æ–≤–∞–Ω–∏—è –ª–∏–Ω–∏–∏
		lda line_x0
		mov b, a
		lda line_x1
		cmp b
		jnc line_ltr 	; x0 > x1, –Ω–µ –Ω–∞–¥–æ –ø–µ—Ä–µ–≤–æ—Ä–∞—á–∏–≤–∞—Ç—å 

		; –ø–æ–º–µ–Ω—è—Ç—å –∫–æ–Ω—Ü—ã –ª–∏–Ω–∏–∏ –º–µ—Å—Ç–∞–º–∏
		lhld line_x0
		xchg 
		lhld line_x1
		shld line_x0
		xchg
		shld line_x1

line_ltr:	; –ø–µ—Ä–µ—Å—á–∏—Ç–∞—Ç—å dx, dy
 		; –ø—Ä–∏—Ä–∞—â–µ–Ω–∏—è, 
		; –Ω–∞—á–∞–ª—å–Ω—ã–µ –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã
		; –ø–æ—Ç–æ–º—É —á—Ç–æ –º—ã –ø–æ–º–µ–Ω—è–ª–∏ –º–µ—Å—Ç–∞–º–∏ X –∏ Y
		call line_calc_dx
		call line_calc_dy

line1:		; –ª–∏–Ω–∏—è –±–µ–∑ –¥–ª–∏–Ω—ã, –Ω–µ—á–µ–≥–æ –∏ —Ä–∏—Å–æ–≤–∞—Ç—å
		lda line_dx
		ora a
		rz

		; –Ω–∞—á–∞–ª—å–Ω–æ–µ –∑–Ω–∞—á–µ–Ω–∏–µ D (—Ä–∞–±–æ—Ç–∞–µ—Ç –≤ BC)
		; D = 2 * dy - dx
		cma
		mov e,a
		mvi d,0FFh
		inx d				; de = -line_dx
		push d

		lda line_dy
		mov l, a
		mvi h,0
		dad h
		shld line1_mod_dy+1		; —Å–æ—Ö—Ä–∞–Ω–∏—Ç—å 2*dy –∫–æ–Ω—Å—Ç–∞–Ω—Ç–æ–π

		dad d				; D = 2 * dy - dx
		xthl				; hl = -line_dx
		
		dad h
		xchg				; de = -2*dx
		lhld line1_mod_dy+1	        ; hl = 2*dy
		dad d 				; hl = 2 * dy - 2 * dx
		shld line1_mod_dydx_s+1		; —Å–æ—Ö—Ä–∞–Ω–∏—Ç—å –∫–∞–∫ –∫–æ–Ω—Å—Ç
		shld line1_mod_dydx_g+1		; —Å–æ—Ö—Ä–∞–Ω–∏—Ç—å –∫–∞–∫ –∫–æ–Ω—Å—Ç
		pop d
		

		; –æ—Å–Ω–æ–≤–Ω–æ–π —Ü–∏–∫–ª —Ä–∏—Å–æ–≤–∞–Ω–∏—è –ª–∏–Ω–∏–∏
		; —Ü–∏–∫–ª —Ä–∞–∑–¥–≤–æ–µ–Ω: –æ–¥–Ω–∞ –≤–µ—Ä—Å–∏—è –¥–ª—è –ø–æ–ª–æ–≥–æ–≥–æ —Å–∫–ª–æ–Ω–∞ (_g)
		; –≤—Ç–æ—Ä–∞—è –¥–ª—è –∫—Ä—É—Ç–æ–≥–æ —Å–∫–ª–æ–Ω–∞ (_s)
		; –ø–µ—Ä–µ–∫–ª—é—á–∞—é—Ç—Å—è –æ–Ω–∏ –ø—Ä–∏ –æ—Ü–µ–Ω–∫–µ –∫—Ä—É—Ç–∏–∑–Ω—ã –∑–∞–ø–∏—Å—å—é 
		; –∞–¥—Ä–µ—Å–∞ –≤ line1_switch
		; -----------------------------		
		lhld line_x
		mov c,l	;line_x
		mov b,h	;line_dx

		; –Ω–∞ –≤—Ä–µ–º—è –æ—Å–Ω–æ–≤–Ω–æ–≥–æ —Ü–∏–∫–ª–∞ –ø—Ä–µ—Ä—ã–≤–∞–Ω–∏—è –∑–∞–ø—Ä–µ—â–µ–Ω—ã,
		; —á—Ç–æ–±—ã —Ö—Ä–∞–Ω–∏—Ç—å –∑–Ω–∞—á–µ–Ω–∏–µ 2*dy –≤ SP
		di
		lxi h, 0
		dad sp
		shld line1_finish+1 	; —Å–æ—Ö—Ä–∞–Ω–∏—Ç—å —É–∫–∞–∑–∞—Ç–µ–ª—å —Å—Ç–µ–∫–∞ –¥–ª—è –≤–æ–∑–≤—Ä–∞—Ç–∞
line1_mod_dy 	lxi sp, 0ffffh 		; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥ (2*dy)

		; –ø–µ—Ä–µ—Ö–æ–¥ –≤–Ω—É—Ç—Ä—å —Ç–µ–ª–∞ —Ü–∏–∫–ª–∞
line1_switch	jmp line1_enter_s	; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥ (–ø–æ–ª–æ–≥–∏–π/–∫—Ä—É—Ç–æ–π)

line1_enter_g
		lda line_y
		sta line_y_g+1
		; –ø–æ–¥–≥–æ—Ç–æ–≤–∏—Ç—å –Ω–∞—á–∞–ª—å–Ω–æ–µ –∑–Ω–∞—á–µ–Ω–∏–µ —Ä–µ–≥–∏—Å—Ç—Ä–∞ c
		mvi a, 111b 		; —Å–Ω–∞—á–∞–ª–∞ –≤—ã—á–∏—Å–ª—è–µ–º —Å–º–µ—â–µ–Ω–∏–µ 
		ana c 			; –ø–∏–∫—Å–µ–ª—è –≤ PixelMask (—Å = x)
		mov l,a
		xra c
		rrc
		rrc
		stc
		rar
		mov c,a 		; c = 0x80 | (x >> 3), l = y
		mvi h,PixelMask>>8
		mov a,m
		sta bit_set_g+1

		jmp line1_loop_g


		;------ –ø–æ–ª–æ–≥–∏–π —Ü–∏–∫–ª (g/gentle) -----
line1_then_g:
line1_mod_dydx_g:		
		lxi h, 0ffffh 		; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥: 2*(dy-dx)
		dad d 			; D = D + 2*(dy-dx)
		xchg
		lxi h, line_y_g+1	; hl = &line_y
line1_mod_yinc_g:
		inr m			; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥: line_y += yinc
		
		lda bit_set_g+1 	; one-hot –±–∏—Ç –ø–∏–∫—Å–µ–ª—è
		rrc 			; —Å–¥–≤–∏–Ω—É—Ç—å –≤–ø—Ä–∞–≤–æ (—Å–ª–µ–¥—É—é—â–∏–π X)
		sta bit_set_g+1 	; —Å–æ—Ö—Ä–∞–Ω–∏—Ç—å
		jnc $+4 		; –µ—Å–ª–∏ –ø—Ä–æ–≤–µ—Ä–Ω—É–ª—Å—è —á–µ—Ä–µ–∑ –∫—Ä–∞–π
		inr c 			; —É–≤–µ–ª–∏—á–∏—Ç—å –∞–¥—Ä–µ—Å –∫–æ–ª–æ–Ω–∫–∏

		dcr b			; dx -= 1
		jz line1_finish
line1_loop_g:	; <--- —Ç–æ—á–∫–∞ –≤—Ö–æ–¥–∞ –≤ –ø–æ–ª–æ–≥–∏–π —Ü–∏–∫–ª --->
		
line_y_g	mvi l, 0ffh 		; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥: –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç–∞ y
		mov h, c 		; hl —É–∫–∞–∑—ã–≤–∞—é—Ç –≤ —ç–∫—Ä–∞–Ω
		xra m 			; a = –ø–∞–º—è—Ç—å —Å –ø–∏–∫—Å–µ–ª–µ–º
		mov m,a 		; –∑–∞–ø–∏—Å–∞—Ç—å –≤ –ø–∞–º—è—Ç—å

		; if D > 0
		xra a
		ora d
		jp line1_then_g
line1_else_g: 	; else –æ—Ç if D > 0
		xchg
		dad sp 			; D = D + 2*dy
		xchg

bit_set_g:
		mvi a,80h 		; one-hot –±–∏—Ç –ø–∏–∫—Å–µ–ª—è
		rrc 			; —Å–¥–≤–∏–Ω—É—Ç—å –≤–ø—Ä–∞–≤–æ (—Å–ª–µ–¥—É—é—â–∏–π X)
		sta bit_set_g+1  
		jnc $+4 		; –µ—Å–ª–∏ –ø—Ä–æ–≤–µ—Ä–Ω—É–ª—Å—è —á–µ—Ä–µ–∑ –∫—Ä–∞–π
		inr c 			; —É–≤–µ–ª–∏—á–∏—Ç—å –∞–¥—Ä–µ—Å –∫–æ–ª–æ–Ω–∫–∏

		dcr b			; dx -= 1
		jnz line1_loop_g
		; --- –∫–æ–Ω–µ—Ü —Ç–µ–ª–∞ –ø–æ–ª–æ–≥–æ–≥–æ —Ü–∏–∫–ª–∞ ---
line1_finish	lxi sp, 0ffffh
		ei
		ret

line1_enter_s
		mov a,c
		sta set_y_s+1 		;y
		lda line_y
		mov c,a	 		;x
		
		ani 111b 		; –≤—ã—á–∏—Å–ª–∏—Ç—å –Ω–∞—á–∞–ª—å–Ω–æ–µ –∑–Ω–∞—á–µ–Ω–∏–µ
		mov l,a 		; –∞–¥—Ä–µ—Å–∞ –∫–æ–ª–æ–Ω–∫–∏ ...
		xra c
		rrc
		rrc 
		stc 
		rar 
		sta line_x_s+1 		; 0x8000 | (a / 8)
		mvi h,PixelMask>>8 	; –∏ one-hot –±–∏—Ç–∞, —Å–æ–æ—Ç–≤–µ—Å—Ç–≤—É—é—â–µ–≥–æ
		mov a,m 		; —Ç–µ–∫—É—â–µ–º—É –ø–∏–∫—Å–µ–ª—é
		sta bit_set_s+1
set_y_s:		
		mvi c,0
		
		jmp line1_loop_s

		;------ –∫—Ä—É—Ç–æ–π —Ü–∏–∫–ª (s/steep) -----
line1_then_s:
line1_mod_dydx_s:		
		lxi h, 0ffffh 		; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥: 2*(dy-dx)
		dad d 			; D = D + 2*(dy-dx)
		xchg
		inr c			; y = y + 1
		lda bit_set_s+1		; one-hot –±–∏—Ç –ø–∏–∫—Å–µ–ª—è
line1_mod_xinc_s1:
		rrc 			; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥: xincLo
		sta bit_set_s+1 
		jnc $+7
		lxi h, line_x_s+1
line1_mod_xinc_s2:
		inr m			; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥: xincHi
		
		dcr b			; dx -= 1
		jz line1_finish
line1_loop_s:	; <--- —Ç–æ—á–∫–∞ –≤—Ö–æ–¥–∞ –≤ –∫—Ä—É—Ç–æ–π —Ü–∏–∫–ª --->

line_x_s:
		mvi h, 80h		; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥: –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç–∞ x
		mov l,c			; –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç–∞ y
		xra m 			; a = –ø–∞–º—è—Ç—å —Å –ø–∏–∫—Å–µ–ª–µ–º
		mov m,a 		; –∑–∞–ø–∏—Å–∞—Ç—å –≤ –ø–∞–º—è—Ç—å

		; if D > 0
		xra a
		ora d
		jp line1_then_s
line1_else_s: 	; else –æ—Ç if D > 0
		xchg
		dad sp 			; D = D + 2*dy
		xchg 			; de = D, hl = 
		inr c			; y = y + 1
		dcr b			; dx -= 1
bit_set_s:
		mvi a,80h 		; one-hot –±–∏—Ç –ø–∏–∫—Å–µ–ª—è
		jnz line1_loop_s
		; --- –∫–æ–Ω–µ—Ü —Ç–µ–ª–∞ –∫—Ä—É—Ç–æ–≥–æ —Ü–∏–∫–ª–∞ ---
		jmp line1_finish
		; --- –∫–æ–Ω–µ—Ü line() ---
		

		; –≤—ã—á–∏—Å–ª–µ–Ω–∏–µ —Ä–∞—Å—Å—Ç–æ—è–Ω–∏—è –ø–æ X (x0 <= x1)
line_calc_dx:
		; –ø—Ä–æ–≤–µ—Ä–∏—Ç—å, —á—Ç–æ x0 <= x1
		lda line_x0
		mov b, a
		lda line_x1
		cmp b
		jnc line_x_positive

		lda line_x0
		sta line_x
		lda line_x1
		mov b, a
		lda line_x0
		sub b
		sta line_dx		
		ret
line_x_positive:
		; dx = x1 - x0
		lda line_x0
		sta line_x
		mov b, a
		lda line_x1
		sub b 			; a = x1 - x0
		sta line_dx
		ret

		; –≤—ã—á–∏—Å–ª–µ–Ω–∏–µ —Ä–∞—Å—Å—Ç–æ—è–Ω–∏—è –ø–æ Y (y0 <= y1)
line_calc_dy:
		; –µ—Å–ª–∏ y0 <= y1
		lda line_y0
		mov b, a
		lda line_y1
		cmp b
		jnc line_y_positive

		; –ø—Ä–∏—Ä–∞—â–µ–Ω–∏–µ y = -1
		mvi a, 035h 		; DCR M
		sta line1_mod_yinc_g
		sta line1_mod_xinc_s2
		mvi a, 007h 		; rlc
		sta line1_mod_xinc_s1

		lda line_y0
		sta line_y
		lda line_y1
		mov b, a
		lda line_y0
		sub b
		sta line_dy		
		ret
line_y_positive:
		mvi a, 034h 		; INR M
		sta line1_mod_yinc_g
		sta line1_mod_xinc_s2
		mvi a, 00Fh 		; rrc
		sta line1_mod_xinc_s1
		
		lda line_y0
		sta line_y
		mov b, a
		lda line_y1
		sub b
		sta line_dy
		ret


Cls:
		lxi	h,08000h
		mvi	e,0
		xra	a
ClrScr:
		mov	m,e
		inx	h
		cmp	h
		jnz	ClrScr
		ret

		; –≤—ã—Ö–æ–¥:
		; HL - —á–∏—Å–ª–æ –æ—Ç 1 –¥–æ 65535
rnd16:
		lxi h,65535
		dad h
		shld rnd16+1
		rnc
		mvi a,00000001b ;–ø–µ—Ä–µ–≤–µ—Ä–Ω—É–ª 80h - 10000000b
		xra l
		mov l,a
		mvi a,01101000b	;–ø–µ—Ä–µ–≤–µ—Ä–Ω—É–ª 16h - 00010110b
		xra h
		mov h,a
		shld rnd16+1
		ret

colors:
;		.db 0,0,0,0,0,0,0,0
		.db 00000000b,00001001b,00010010b,00011011b,00100100b,00101101b,00110110b,00111111b
		.db 11111111b,00001001b,00010010b,00011011b,00100100b,00101101b,00110110b,00111111b

		.end