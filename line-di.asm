		; üêü —Ä–∏—Å–æ–≤–∞–Ω–∏—è –ª–∏–Ω–∏–∏ –∞–ª–≥–æ—Ä–∏—Ç–º–æ–º –ë—Ä–µ–∑–µ–Ω—Ö–µ–º–∞ –≤ —Ä–µ–∂–∏–º–µ 256—Ö256
		; –¢–µ—Å—Ç –∏ –±–µ–Ω—á–º–∞—Ä–∫
		; 
		; –î–ª—è –∑–∞–ø—É—Å–∫–∞ –±–µ–Ω—á–º–∞—Ä–∫–∞ –Ω–∞–∂–∞—Ç—å –£–° / –°–° / –†–£–°–õ–ê–¢
		; (–≤ —Å—Ä–µ–¥–Ω–µ–º 246 –ª–∏–Ω–∏–π –≤ —Å–µ–∫—É–Ω–¥—É)
		;
		; –¢–æ—á–∫–∞ –≤—Ö–æ–¥–∞ –ø—Ä–æ—Ü–µ–¥—É—Ä—ã —Ä–∏—Å–æ–≤–∞–Ω–∏—è: 
		; 	line
		; –í—Ö–æ–¥–Ω—ã–µ –ø–∞—Ä–∞–º–µ—Ç—Ä—ã:
		; 	line_x0, line_y0, line_x1, line_y1
		; –†–∏—Å–æ–≤–∞–Ω–∏–µ –ø—Ä–æ–∏—Å—Ö–æ–¥–∏—Ç –≤ –ø–ª–æ—Å–∫–æ—Å—Ç–∏ 0x8000
		;
		; –Ω–∞ –≤—Ä–µ–º—è —Ä–∏—Å–æ–≤–∞–Ω–∏–∏ –ª–∏–Ω–∏–π –ø—Ä–µ—Ä—ã–≤–∞–Ω–∏—è –∑–∞–ø—Ä–µ—â–∞—é—Ç—Å—è (–ø–æ—Ç–æ–º —Ä–∞–∑—Ä–µ—à–∞—é—Ç—Å—è)
		;
		; –í—è—á–µ—Å–ª–∞–≤ –°–ª–∞–≤–∏–Ω—Å–∫–∏–π –∏ –ò–≤–∞–Ω –ì–æ—Ä–æ–¥–µ—Ü–∫–∏–π, 2017
                ;

                .nodump
                .binfile line-ei.rom
                .tape v06c-rom
		.org 100h

		di
start:
		xra	a
		out	10h
		lxi	sp,100h
		mvi	a,0C3h
		sta	0
		lxi	h,Restart
		shld	1

		call	Cls
		mvi	a,0C9h
		sta	38h
		ei
		hlt
		lxi	h, colors+15
colorset:
		mvi	a, 88h
		out	0
		mvi	c, 15
colorset1:	mov	a, c
		out	2
		mov	a, m
		out	0Ch
		dcx	h
		out	0Ch
		out	0Ch
		dcr	c
		out	0Ch
		out	0Ch
		out	0Ch
		jp	colorset1
		mvi	a,255
		out	3


Restart:
		call	Cls

		call SetPixelModeOR
		
;—Ä–∞–º–∫–∞ –ø–æ –∫—Ä–∞—é		
		lxi h,00000h
		shld line_x0
		lxi h,00FFh
		shld line_x1
		call line
		lxi h,00000h
		shld line_x0
		lxi h,0FF00h
		shld line_x1
		call line
		
		lxi h,0FFFFh
		shld line_x0
		lxi h,00FFh
		shld line_x1
		call line
		lxi h,0FFFFh
		shld line_x0
		lxi h,0FF00h
		shld line_x1
		call line

;–≤–Ω—É—Ç—Ä–µ–Ω—è—è —Ä–∞–º–∫–∞
		lxi h,01010h
		shld line_x0
		lxi h,010F0h
		shld line_x1
		call line
		lxi h,01010h
		shld line_x0
		lxi h,0F010h
		shld line_x1
		call line

		lxi h,0F0F0h
		shld line_x0
		lxi h,0F010h
		shld line_x1
		call line
		lxi h,0F0F0h
		shld line_x0
		lxi h,010F0h
		shld line_x1
		call line

;—É–≥–æ–ª–∫–∏		
		lxi h,00808h
		shld line_x0
		lxi h,00908h
		shld line_x1
		call line
		lxi h,00808h
		shld line_x0
		lxi h,00809h
		shld line_x1
		call line

		lxi h,0F8F8h
		shld line_x0
		lxi h,0F8F7h
		shld line_x1
		call line
		lxi h,0F8F8h
		shld line_x0
		lxi h,0F7F8h
		shld line_x1
		call line
		
		lxi h,008F8h
		shld line_x0
		lxi h,009F8h
		shld line_x1
		call line
		lxi h,008F8h
		shld line_x0
		lxi h,008F7h
		shld line_x1
		call line

		lxi h,0F808h
		shld line_x0
		lxi h,0F809h
		shld line_x1
		call line
		lxi h,0F808h
		shld line_x0
		lxi h,0F708h
		shld line_x1
		call line

;—Ç–æ—á–∫–∏
		lxi h,02020h
		shld line_x0
		shld line_x1
		call line

		lxi h,0DF20h
		shld line_x0
		shld line_x1
		call line

		lxi h,020DFh
		shld line_x0
		shld line_x1
		call line

		lxi h,0DFDFh
		shld line_x0
		shld line_x1
		call line
		
		lxi h,circl
circloop:
		push h
		lxi h,8080h
		shld line_x0
		pop h
		mov a,m
		ora a
		jz benchmark
		sta line_x1
		inx h
		mov a,m
		sta line_y1
		inx h
		push h
		call line
		pop h
		jmp circloop

circl:
		.db 228,128
		.db 220,166
		.db 199,199
		.db 166,220
		.db 128,228
		.db 90,220
		.db 57,199
		.db 36,166
		.db 28,128
		.db 36,90
		.db 57,57
		.db 90,36
		.db 128,28
		.db 166,36
		.db 199,57
		.db 220,90
		.db 0

benchmark
		in 1
		rlc
		jnc benchmark_go
		rlc
		jnc benchmark_go
		rlc
		jc benchmark

benchmark_go
		call SetPixelModeXOR

		call rnd16
		shld line_tail
foreva:
		lhld line_tail
		shld line_x0

		call rnd16
		mov d, h
		mov e, l
		inx d
		mov a, d
		ora e
		jz foreva_nomoar
		shld line_x1
		shld line_tail

		call line
		jmp foreva
foreva_nomoar
		jmp Restart

line_tail:	.dw 0

		; –∞—Ä–≥—É–º–µ–Ω—Ç—ã line()
line_x0		.db 100
line_y0		.db 55
line_x1		.db 0
line_y1		.db 50 

		; —ç—Ç–∏ —á–µ—Ç—ã—Ä–µ –±–∞–π—Ç–∞ –¥–æ–ª–∂–Ω—ã –∏–¥—Ç–∏ –≤ —Ç–∞–∫–æ–º –ø–æ—Ä—è–¥–∫–µ, –∞ —Ç–æ
line_y		.db 0
line_x		.db 0
line_dx 	.db 0
line_dy		.db 0

SetPixelModeOR:
		mvi a,0B1h			;ora c
		sta SetPixelMode_g1
		mvi a,0B6h			;ora m
		sta SetPixelMode_s2
		sta SetPixelMode_g2
		sta SetPixelMode_g3
		mvi a,0F6h			;ori D8
		sta SetPixelMode_s1
		ret

SetPixelModeXOR:
		mvi a,0A9h			;xra c
		sta SetPixelMode_g1
		mvi a,0AEh			;xra m
		sta SetPixelMode_s2
		sta SetPixelMode_g2
		sta SetPixelMode_g3
		mvi a,0EEh			;xri D8
		sta SetPixelMode_s1
		ret

PixelMask:
		.db 10000000b
		.db 01000000b
		.db 00100000b
		.db 00010000b
		.db 00001000b
		.db 00000100b
		.db 00000010b
		.db 00000001b
line:		; –≤—ã—á–∏—Å–ª–∏—Ç—å line_dx, line_dy –∏ –ø—Ä–∏—Ä–∞—â–µ–Ω–∏–µ Y
		; line_dx >= 0, line_dy >= 0, line1_mod_yinc ? [-1,1]
		call line_calc_dx 
		call line_calc_dy

		; –ø—Ä–æ–≤–µ—Ä—è–µ–º –∫—Ä—É—Ç–∏–∑–Ω—É —Å–∫–ª–æ–Ω–∞:
		; dy >= 0, dx >= 0
		;  	dy <= dx 	?	–ø–æ–ª–æ–≥–∏–π
		;	dy > dx 	?	–∫—Ä—É—Ç–æ–π
		lhld line_dx 	        ; l = dx, h = dy
		mov a, l 
		cmp h		        ;–µ—Å–ª–∏ dy<=dx
		jnc  line_gentle	;—Ç–æ —Å–∫–ª–æ–Ω –ø–æ–ª–æ–≥–∏–π
		
		; –µ—Å–ª–∏ —Å–∫–ª–æ–Ω –∫—Ä—É—Ç–æ–π,
		; —Ç–æ –º–µ–Ω—è–µ–º –º–µ—Å—Ç–∞–º–∏ x0 –∏ y0
		lhld line_x0 		;  l = x, h = y
		mov d, l 	 	;  d = y
		mov e, h 	 	;  e = x
		xchg		 	;  l = y, h = x
		shld line_x0
		; –∏ –º–µ–Ω—è–µ–º –º–µ—Å—Ç–∞–º–∏ x1 –∏ y1
		lhld line_x1 		;  l = x, h = y
		mov d, l 		;  d = y
		mov e, h 		;  e = x
		xchg			;  l = y, h = x
		shld line_x1
		; –∫—Ä—É—Ç–æ–π —Å–∫–ª–æ–Ω: –ø–µ—Ä–µ–≤–æ–¥–∏–º —Å—Ç—Ä–µ–ª–∫—É –Ω–∞ —Ü–∏–∫–ª S
		mvi a,0C3h		; jmp
		.db 21h			; lxi h, - –ø—Ä–æ–ø—É—Å–∫–∞–µ–º –ø–µ—Ä–µ–∫–ª—é—á–µ–Ω–∏–µ –Ω–∞ line_gentle
line_gentle:
		; —Å–∫–ª–æ–Ω –ø–æ–ª–æ–≥–∏–π: —Å—Ç—Ä–µ–ª–∫–∞ –Ω–∞ —Ü–∏–∫–ª–µ G
		mvi a,011h		; lxi d,
		sta line1_switch
		; –µ—Å–ª–∏ —Ç–µ–ø–µ—Ä—å –ø–æ–ª—É—á–∏–ª–æ—Å—å —Ç–∞–∫, —á—Ç–æ x0 > x1,
		; –Ω–∞–¥–æ –∏–∑–º–µ–Ω–∏—Ç—å –Ω–∞–ø—Ä–∞–≤–ª–µ–Ω–∏–µ —Ä–∏—Å–æ–≤–∞–Ω–∏—è –ª–∏–Ω–∏–∏
		lda line_x0
		mov b, a		;b = x0
		lda line_x1		;a = x1
		cmp b
		jnc line_ltr 	        ; x0 <= x1, –Ω–µ –Ω–∞–¥–æ –ø–µ—Ä–µ–≤–æ—Ä–∞—á–∏–≤–∞—Ç—å 

		; –ø–æ–º–µ–Ω—è—Ç—å –∫–æ–Ω—Ü—ã –ª–∏–Ω–∏–∏ –º–µ—Å—Ç–∞–º–∏
		lhld line_x0	        ;l=x0, h=y0
		xchg 			;e=x0, d=y0
		lhld line_x1	        ;l=x1, h=y1
		shld line_x0
		xchg
		shld line_x1

line_ltr:	; –ø–µ—Ä–µ—Å—á–∏—Ç–∞—Ç—å dx, dy
 		; –ø—Ä–∏—Ä–∞—â–µ–Ω–∏—è, 
		; –Ω–∞—á–∞–ª—å–Ω—ã–µ –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç—ã
		; –ø–æ—Ç–æ–º—É —á—Ç–æ –º—ã –ø–æ–º–µ–Ω—è–ª–∏ –º–µ—Å—Ç–∞–º–∏ X –∏ Y
		call line_calc_dx
		call line_calc_dy

line1:
		; –Ω–∞—á–∞–ª—å–Ω–æ–µ –∑–Ω–∞—á–µ–Ω–∏–µ D
		; D = 2 * dy - dx
		lda line_dx
		cma
		mov e,a
		mvi d,0FFh
		inx d			; de = -dx

		lhld line_dy
		mvi h,0
		dad h
		shld line1_mod_dy_g+1   ; —Å–æ—Ö—Ä–∞–Ω–∏—Ç—å 2*dy –∫–æ–Ω—Å—Ç–∞–Ω—Ç–æ–π
		shld line1_mod_dy_s+1	; —Å–æ—Ö—Ä–∞–Ω–∏—Ç—å 2*dy –∫–æ–Ω—Å—Ç–∞–Ω—Ç–æ–π

		dad d			; hl = 2 * dy - dx
		push h			; –ø–æ–º–µ—Å—Ç–∏—Ç—å –≤ —Å—Ç–µ–∫ –∑–Ω–∞—á–µ–Ω–∏–µ D = 2 * dy - dx
		xchg			; hl = -dx
		
		dad h
		xchg			; de = -2*dx
		lhld line1_mod_dy_g+1	; hl = 2*dy
		dad d 			; hl = 2 * dy - 2 * dx
		shld line1_mod_dydx_s+1	; —Å–æ—Ö—Ä–∞–Ω–∏—Ç—å –∫–∞–∫ –∫–æ–Ω—Å—Ç
		shld line1_mod_dydx_g+1	; —Å–æ—Ö—Ä–∞–Ω–∏—Ç—å –∫–∞–∫ –∫–æ–Ω—Å—Ç

                ;! –≤ —Å—Ç–µ–∫–µ –æ—Å—Ç–∞–ª–æ—Å—å –∑–Ω–∞—á–µ–Ω–∏–µ D = 2 * dy - dx

		; –æ—Å–Ω–æ–≤–Ω–æ–π —Ü–∏–∫–ª —Ä–∏—Å–æ–≤–∞–Ω–∏—è –ª–∏–Ω–∏–∏
		; —Ü–∏–∫–ª —Ä–∞–∑–¥–≤–æ–µ–Ω: –æ–¥–Ω–∞ –≤–µ—Ä—Å–∏—è –¥–ª—è –ø–æ–ª–æ–≥–æ–≥–æ —Å–∫–ª–æ–Ω–∞ (_g)
		; –≤—Ç–æ—Ä–∞—è –¥–ª—è –∫—Ä—É—Ç–æ–≥–æ —Å–∫–ª–æ–Ω–∞ (_s)
		; –ø–µ—Ä–µ–∫–ª—é—á–∞—é—Ç—Å—è –æ–Ω–∏ –ø—Ä–∏ –æ—Ü–µ–Ω–∫–µ –∫—Ä—É—Ç–∏–∑–Ω—ã –∑–∞–ø–∏—Å—å—é 
		; –∞–¥—Ä–µ—Å–∞ –≤ line1_switch
		; -----------------------------		
		lhld line_x
		mov c,l		;–¥–ª—è –ø–æ–ª–æ–≥–æ–≥–æ —ç—Ç–æ x (–∞ –¥–ª—è –∫—Ä—É—Ç–æ–≥–æ —Ü–∏–∫–ª–∞ —ç—Ç–æ y)
		lda line_y	;–¥–ª—è –ø–æ–ª–æ–≥–æ–≥–æ —ç—Ç–æ y (–∞ –¥–ª—è –∫—Ä—É—Ç–æ–≥–æ —Ü–∏–∫–ª–∞ —ç—Ç–æ x)

		; –ø–µ—Ä–µ—Ö–æ–¥ –≤–Ω—É—Ç—Ä—å —Ç–µ–ª–∞ —Ü–∏–∫–ª–∞
line1_switch	jmp line1_enter_s	; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥ (lxi d –µ—Å–ª–∏ –ø–æ–ª–æ–≥–∏–π/jmp –µ—Å–ª–∏ –∫—Ä—É—Ç–æ–π)

line1_enter_g
		mov b,h		        ;line_dx
		sta line_yx_g+1
		; –ø–æ–¥–≥–æ—Ç–æ–≤–∏—Ç—å –Ω–∞—á–∞–ª—å–Ω–æ–µ –∑–Ω–∞—á–µ–Ω–∏–µ —Ä–µ–≥–∏—Å—Ç—Ä–∞ c
		mvi a, 111b 		; —Å–Ω–∞—á–∞–ª–∞ –≤—ã—á–∏—Å–ª—è–µ–º —Å–º–µ—â–µ–Ω–∏–µ 
		ana c 			; –ø–∏–∫—Å–µ–ª—è –≤ PixelMask (—Å = x)
		adi PixelMask&255
		mov l,a
		mvi a,PixelMask>>8
		aci 0
		mov h,a			; hl - –∞–¥—Ä–µ—Å –º–∞—Å–∫–∏ –≤ PixelMask
		mvi a,11111000b
		ana c
		rrc
		rrc
		stc
		rar
		sta line_yx_g+2	        ; 0x80 | (x >> 3), l = y

		xra a
		cmp b			; dx=0?
		mov a,m			; –º–∞—Å–∫–∞
		pop d			; de = 2 * dy - dx
		jz setlastpixel_g	; –µ—Å–ª–∏ dx=0, —Ç–æ —Å—Ç–∞–≤–∏–º –æ–¥–Ω—É —Ç–æ—á–∫—É
		di
		lxi h,0
		dad sp
		shld restore_sp_g+1
		xchg		        ; hl = 2 * dy - dx
line_yx_g:
		lxi d, 0 	        ; de —É–∫–∞–∑—ã–≤–∞–µ—Ç –≤ —ç–∫—Ä–∞–Ω
		
		jmp line1_loop_g	; –µ—Å–ª–∏ dx<>0, —Ç–æ –ø–µ—Ä–µ—Ö–æ–¥ –Ω–∞ –æ–±—ã—á–Ω–æ–µ —Ä–∏—Å–æ–≤–∞–Ω–∏–µ –ª–∏–Ω–∏–∏

		;------ –ø–æ–ª–æ–≥–∏–π —Ü–∏–∫–ª (g/gentle) -----
line1_then_g:
line1_mod_dydx_g:		
		lxi sp, 0ffffh 		; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥: 2*(dy-dx)
		dad sp 			; D = D + 2*(dy-dx)
line1_mod_yinc_g:
		inr e			; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥: line_y += yinc –∏–ª–∏ line_y -= yinc
		
		mov a,c
		rrc 			; —Å–¥–≤–∏–Ω—É—Ç—å –≤–ø—Ä–∞–≤–æ (—Å–ª–µ–¥—É—é—â–∏–π X)
		jnc $+4 		; –µ—Å–ª–∏ –Ω–µ –ø—Ä–æ–≤–µ—Ä–Ω—É–ª—Å—è —á–µ—Ä–µ–∑ –∫—Ä–∞–π
		inr d			; line_x += 1

		dcr b			; dx -= 1
		jz setlastpixel_g2
line1_loop_g:	; <--- —Ç–æ—á–∫–∞ –≤—Ö–æ–¥–∞ –≤ –ø–æ–ª–æ–≥–∏–π —Ü–∏–∫–ª --->
		mov c,a			; —Å–æ—Ö—Ä–∞–Ω—è–µ–º –∑–Ω–∞—á–µ–Ω–∏–µ –±–∏—Ç–∞
		ldax d
SetPixelMode_g1:
		xra c
		stax d			; –∑–∞–ø–∏—Å–∞—Ç—å –≤ –ø–∞–º—è—Ç—å

		; if D > 0
		xra a
		ora h
		jp line1_then_g
line1_else_g: 	; else –æ—Ç if D > 0
line1_mod_dy_g
		lxi sp, 0ffffh 		; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥ (2*dy)
		dad sp			; D = D + 2*dy

		mov a,c
		rrc 			; —Å–¥–≤–∏–Ω—É—Ç—å –≤–ø—Ä–∞–≤–æ (—Å–ª–µ–¥—É—é—â–∏–π X)
		jnc $+4 		; –µ—Å–ª–∏ –Ω–µ –ø—Ä–æ–≤–µ—Ä–Ω—É–ª—Å—è —á–µ—Ä–µ–∑ –∫—Ä–∞–π
		inr d			; line_x += 1
		
		dcr b			; dx -= 1
		jnz line1_loop_g
		; --- –∫–æ–Ω–µ—Ü —Ç–µ–ª–∞ –ø–æ–ª–æ–≥–æ–≥–æ —Ü–∏–∫–ª–∞ ---
setlastpixel_g2:
		xchg
SetPixelMode_g2:
		xra m
		mov m,a 		; –∑–∞–ø–∏—Å–∞—Ç—å –≤ –ø–∞–º—è—Ç—å
restore_sp_g:
		lxi sp,0
		ei
		ret

setlastpixel_g:
		lhld line_yx_g+1 ; hl —É–∫–∞–∑—ã–≤–∞–µ—Ç –≤ —ç–∫—Ä–∞–Ω
SetPixelMode_g3:
		xra m
		mov m,a 		; –∑–∞–ø–∏—Å–∞—Ç—å –≤ –ø–∞–º—è—Ç—å
		ret


line1_enter_s:
		sta set_x+1		; x
		ani 111b 		; –≤—ã—á–∏—Å–ª–∏—Ç—å –Ω–∞—á–∞–ª—å–Ω–æ–µ –∑–Ω–∞—á–µ–Ω–∏–µ –∞–¥—Ä–µ—Å–∞ –∫–æ–ª–æ–Ω–∫–∏ ...
		adi PixelMask&255
		mov l,a
		mvi a,PixelMask>>8
		aci 0
		mov h,a			; hl - –∞–¥—Ä–µ—Å –º–∞—Å–∫–∏ –≤ PixelMask
		mvi a,11111000b
set_x
		ani 0
		rrc
		rrc 
		stc 
		rar 
		mov b,a			; –∫–æ–æ—Ä–¥–∏–Ω–∞—Ç–∞ x: 0x8000 | (a / 8)

		lda line_x1
		sta last_y_s1+1
		sta last_y_s2+1
		mov a,m 		; –Ω–∞—á–∞–ª—å–Ω–æ–µ –∑–Ω–∞—á–µ–Ω–∏–µ –ø–∏–∫—Å–µ–ª—è
		sta bit_set_s+1
		pop d			; de = 2 * dy - dx

		di
		lxi h,0
		dad sp
		shld restore_sp_s+1
		xchg			; hl = 2 * dy - dx
line1_mod_dydx_s:		
		lxi d, 0ffffh 		; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥: 2*(dy-dx)
line1_mod_dy_s:
		lxi sp, 0ffffh 		; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥ (2*dy)
		
		jmp line1_loop_s

		;------ –∫—Ä—É—Ç–æ–π —Ü–∏–∫–ª (s/steep) -----
line1_then_s:
;line1_mod_dydx_s:		
;		lxi d, 0ffffh 		; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥: 2*(dy-dx)
		dad d 			; D = D + 2*(dy-dx)
		inr c			; y = y + 1
		lda bit_set_s+1		; one-hot –±–∏—Ç –ø–∏–∫—Å–µ–ª—è
line1_mod_xinc_s1:
		rrc 			; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥: xincLo
		sta bit_set_s+1 
		jnc $+4
line1_mod_xinc_s2:
		inr b			; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥: xincHi
		
last_y_s1:
		mvi a,0
		cmp c
		jz setlastpixel_s

line1_loop_s:	; <--- —Ç–æ—á–∫–∞ –≤—Ö–æ–¥–∞ –≤ –∫—Ä—É—Ç–æ–π —Ü–∏–∫–ª --->
		ldax b
SetPixelMode_s1:
bit_set_s:
		xri 0
		stax b	 		; –∑–∞–ø–∏—Å–∞—Ç—å –≤ –ø–∞–º—è—Ç—å —Ä–µ–∑—É–ª—å—Ç–∞—Ç —Å –∏–∑–º–µ–Ω–µ–Ω–Ω—ã–º –ø–∏–∫—Å–µ–ª–µ–º

		; if D > 0
		xra a
		ora h
		jp line1_then_s
line1_else_s: 	; else –æ—Ç if D > 0
;line1_mod_dy_s:
;		lxi d, 0ffffh 		; –∏–∑–º–µ–Ω—è–µ–º—ã–π –∫–æ–¥ (2*dy)
;		dad d 			; D = D + 2*dy
		dad sp 			; D = D + 2*dy
		inr c			; y = y + 1
last_y_s2:
		mvi a,0
		cmp c
		jnz line1_loop_s
		; --- –∫–æ–Ω–µ—Ü —Ç–µ–ª–∞ –∫—Ä—É—Ç–æ–≥–æ —Ü–∏–∫–ª–∞ ---
setlastpixel_s:
		lxi h, bit_set_s+1 ; —É–∫–∞–∑–∞—Ç–µ–ª—å –Ω–∞ —Ç–µ–∫—É—â—É—é –º–∞—Å–∫—É –ø–∏–∫—Å–µ–ª—è
		ldax b
SetPixelMode_s2:
		xra m
		stax b			; –∑–∞–ø–∏—Å–∞—Ç—å –≤ –ø–∞–º—è—Ç—å —Ä–µ–∑—É–ª—å—Ç–∞—Ç —Å –∏–∑–º–µ–Ω–µ–Ω–Ω—ã–º –ø–∏–∫—Å–µ–ª–µ–º
restore_sp_s:
		lxi sp,0
		ei
		ret
		; --- –∫–æ–Ω–µ—Ü line() ---
		

		; –≤—ã—á–∏—Å–ª–µ–Ω–∏–µ —Ä–∞—Å—Å—Ç–æ—è–Ω–∏—è –ø–æ X (dx)
line_calc_dx:
		; –ø—Ä–æ–≤–µ—Ä–∏—Ç—å, —á—Ç–æ x0 <= x1
		lda line_x0
		sta line_x
		mov b, a		;b = x0
		lda line_x1
		sub b			;a = x1 - x0
		jnc line_x_positive ;–µ—Å–ª–∏ x0 <= x1, —Ç–æ –ø–µ—Ä–µ—Ö–æ–¥

		;–µ—Å–ª–∏ x0 > x1, —Ç–æ –ø—Ä–∏—à–ª–∏ —Å—é–¥–∞
		cma
		inr a			; -(x1-x0)=x0-x1

line_x_positive:
		sta line_dx		; —Å–æ—Ö—Ä–∞–Ω–∏–ª–∏ dx (–æ–Ω –ø–æ–ª–æ–∂–∏—Ç–µ–ª—å–Ω—ã–π)
		ret

		; –≤—ã—á–∏—Å–ª–µ–Ω–∏–µ —Ä–∞—Å—Å—Ç–æ—è–Ω–∏—è –ø–æ Y (dy)
line_calc_dy:
		; –µ—Å–ª–∏ y0 <= y1
		lda line_y0
		sta line_y
		mov b, a		;b = y0
		lda line_y1
		sub b			;a = y1 - y0
		jnc line_y_positive	;–µ—Å–ª–∏ y0 <= y1, —Ç–æ –ø–µ—Ä–µ—Ö–æ–¥

		;–µ—Å–ª–∏ y0 > y1, —Ç–æ –ø—Ä–∏—à–ª–∏ —Å—é–¥–∞
		cma
		inr a			; -(y1-y0)= y0 - y1
		sta line_dy		; —Å–æ—Ö—Ä–∞–Ω–∏–ª–∏ dy (–æ–Ω –ø–æ–ª–æ–∂–∏—Ç–µ–ª—å–Ω—ã–π)
		
		; –ø—Ä–∏—Ä–∞—â–µ–Ω–∏–µ y = -1
		mvi a, 01Dh 		; dcr e
		sta line1_mod_yinc_g
		mvi a, 005h		; dcr b
		sta line1_mod_xinc_s2
		mvi a, 007h 		; rlc
		sta line1_mod_xinc_s1
		ret

line_y_positive:
		sta line_dy	        ; y1 - y0

		mvi a, 01Ch 		; inr e
		sta line1_mod_yinc_g
		mvi a, 004h		; inr b
		sta line1_mod_xinc_s2
		mvi a, 00Fh 		; rrc
		sta line1_mod_xinc_s1
		ret


Cls:
		lxi	h,08000h
		mvi	e,0
		xra	a
ClrScr:
		mov	m,e
		inx	h
		cmp	h
		jnz	ClrScr
		ret

		; –≤—ã—Ö–æ–¥:
		; HL - —á–∏—Å–ª–æ –æ—Ç 1 –¥–æ 65535
rnd16:
		lxi h,65535
		dad h
		shld rnd16+1
		rnc
		mvi a,00000001b ;–ø–µ—Ä–µ–≤–µ—Ä–Ω—É–ª 80h - 10000000b
		xra l
		mov l,a
		mvi a,01101000b	;–ø–µ—Ä–µ–≤–µ—Ä–Ω—É–ª 16h - 00010110b
		xra h
		mov h,a
		shld rnd16+1
		ret

colors:
		.db 00000000b,00001001b,00010010b,00011011b,00100100b,00101101b,00110110b,00111111b
		.db 11111111b,00001001b,00010010b,00011011b,00100100b,00101101b,00110110b,00111111b

		.end
