	                        ; 🐟 для Океана-240
	                        ; Для загрузки в монитор через директиву L
	                        ; Автор: Тимур Ташпулатов, C.-Петербург 2018
	                	.project okeah
	                	.org 8000h
	                
	                BANKING	equ	0c1h		; регистр управления банками ОЗУ и ПЗУ
	                VIDEO	equ	0e1h		; регистр управления цветом и режимами видео
	                
8000	3E 01           	mvi	a, 01
8002	D3 C1           	out	BANKING		; включить отображение старших 32К на младшие
	                
8004	AF              	xra	a		; очистить аккумулятор
8005	21 00 40        	lxi	h, 4000h	; поместить в HL адрес начала экранной области
8008	16 00           	mvi	d, 0		; очистить регистр D можно было бы на один байт короче
	                
800A	01 00 20        	lxi	b, 256*32	; экран в "Океане" состоит из вертикальных столбиков по 256 байт в каждом
800D	72              Loop:	mov	m, d		; заполняем экран трухой
800E	14              	inr	d		; примерно на треть
800F	23              	inx 	h		; двигаться дальше
	                
8010	0B              	dcx 	b		; внимательно следим за длиной столбика
8011	78              	mov	a, b		; если столбик 
8012	B1              	ora	c		; не дорисован
8013	C2 0D 80        	jnz	Loop		; продолжаем писать в экран труху
	                
8016	21 00 41        	lxi	h, 4100h	; цвет каждой точки определяется битами соседних через 100h байт
8019	3E 00           	mvi	a, 00h		; первый столбик
801B	CD 53 80        	call	Strip		; окрашиваем нулём
	                
801E	21 00 43        	lxi	h, 4300h	; второй столбик
8021	3E 0F           	mvi	a, 0fh		; окрашиваем в две полосы
8023	CD 53 80        	call	Strip		; для красоты
	                
8026	21 00 45        	lxi	h, 4500h	; третье окно
8029	3E 33           	mvi	a, 33h		; выходит
802B	CD 53 80        	call	Strip		; к океану
	                
802E	21 00 47        	lxi	h, 4700h	; ровным ветром
8031	3E 55           	mvi	a, 55h		; дышит
8033	CD 53 80        	call	Strip		; океан
	                
8036	21 00 49        	lxi	h, 4900h	; а за ним
8039	3E FF           	mvi	a,0ffh		; диковинные
803B	CD 53 80        	call	Strip		; страны
	                
	                				; и никто
	                				; не видел
	                				; этих стран
	                
803E	AF              	xra	a		; экран размалеван
803F	D3 C1           	out	BANKING		; возвращаем его на место
	                
8041	3C              Key:	inr	a		; не пропадать же аккумулятору
8042	E6 3F           	ani	3fh		; возьмем из него три младших бита переднего плана
8044	F6 40           	ori	40h		; и три старших бита фона
8046	D3 E1           	out	VIDEO		; и намажем на кадр
	                
8048	CD 09 E0        	call	0e009h		; подождем ввода символа с клавиатуры
804B	FE 1B           	cpi	1bh		; если это не Esc,
804D	C2 41 80        	jnz	Key		; продолжим интерактивную раскраску
	                
8050	C3 03 E0        	jmp	0e003h		; теплый старт "Монитора"
	                
8053	77              Strip:	mov	m, a		; это подпрограмма
8054	23              	inx	h		; небыстрого, но верного
8055	15              	dcr	d		; рисования столбика
8056	C2 53 80        	jnz	Strip		; с заданною длиною
	                
8059	C9              	ret			; К О Н Е Ц
	                
Labels:
banking                 00C1
key                     8041	loop                    800D
strip                   8053	video                   00E1

Memory dump:
 
8000:  3E 01 D3 C1 AF 21 00 40-16 00 01 00 20 72 14 23  >....!.@.....r.#
8010:  0B 78 B1 C2 0D 80 21 00-41 3E 00 CD 53 80 21 00  .x....!.A>..S.!.
8020:  43 3E 0F CD 53 80 21 00-45 3E 33 CD 53 80 21 00  C>..S.!.E>3.S.!.
8030:  47 3E 55 CD 53 80 21 00-49 3E FF CD 53 80 AF D3  G>U.S.!.I>..S...
8040:  C1 3C E6 3F F6 40 D3 E1-CD 09 E0 FE 1B C2 41 80  .<.?.@........A.
8050:  C3 03 E0 77 23 15 C2 53-80 C9                    ...w#..S........
